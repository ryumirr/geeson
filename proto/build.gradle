plugins {
    id 'com.google.protobuf' version '0.8.19'  // 최신 안정화된 버전
}

group = 'proto'
version = '0.0.1'

dependencies {
    api 'com.google.protobuf:protobuf-java:3.21.7'  // 여기만 api로 변경, 다른 모듈에서 사용
    implementation 'io.grpc:grpc-protobuf:1.59.0'
    implementation 'io.grpc:grpc-stub:1.59.0'
    implementation 'io.grpc:grpc-netty-shaded:1.59.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs = [
                'src/main/java',
                'build/generated/source/proto/main/java',
                'build/generated/source/proto/main/grpc'
            ]
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.21.7'  // 최신 버전
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.59.0'  // 최신 버전
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = 'java'
                }
            }
            task.plugins {
                grpc {
                    outputSubDir = 'grpc'
                }
            }
        }
    }
}

tasks.named('processResources') {
    exclude('**/*.proto') // .proto 파일은 리소스로 복사되지 않도록
}

tasks.named('bootJar') {
    enabled = false
}

tasks.named('compileJava') {
    dependsOn tasks.named('generateProto')
}
